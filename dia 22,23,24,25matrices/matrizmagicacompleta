//6. Una matriz mágica es una matriz cuadrada (tiene igual número de filas que de columnas) que
//tiene como propiedad especial que la suma de las filas, las columnas y las diagonales es igual.
//Por ejemplo:
//	2 7 6
//	9 5 1
//	4 3 8
//	En la matriz de ejemplo las sumas son siempre 15. Considere el problema de construir un
//	algoritmo que compruebe si una matriz de datos enteros es mágica o no, y en caso de que
//			sea mágica escribir la suma. Además, el programa deberá comprobar que los números
//			introducidos son correctos, es decir, están entre el 1 y el 9. El usuario ingresa el tamaño de la
//			matriz que no debe superar orden igual a 10.


Algoritmo Ej22_6_matrizMagica_
	Definir matriz, n,m, suma Como Entero
	
	Escribir "ingrese el numéro de filas"
	leer n
	m = n
	
	Mientras n<1 o n>3
		Escribir "La matriz no puede ser de orden superior a 10, es decir las filas no pueden ser mayor a 3"
		Escribir "ingrese el numéro de filas"
		leer n
		m=n
	FinMientras
	
	Dimension matriz(n,m)
	
	llenarMatriz(matriz, n,m)
	//	llenarMatrizMagica(matriz, n,m)	
	
	Escribir ""
	Escribir "la matriz ingresada es "
	mostrarMatriz(matriz, n,m)
	
	Escribir ""
	Escribir "*** Resultado"
	matrizMagica(matriz, n,m)
FinAlgoritmo



SubProceso llenarMatriz(matriz, n,m)
	Definir i, j Como Entero
	
	Para i<- 0 hasta n-1
		Para j<- 0 hasta m-1
			Escribir "Digite un número entre 1 y 9, para la posición (" i "," j ")"
			leer matriz(i,j)
			mientras matriz(i,j)<1 o matriz(i,j)>9
				Escribir sin saltar "El número no esta entre 1 y 9, Digite nuevamente el numero"
				leer matriz(i,j)
			FinMientras
		FinPara
	FinPara
	
FinSubProceso


SubProceso  mostrarMatriz(matriz, n,m)
	Definir i, j Como Entero
	
	Para i<- 0 hasta n-1
		Para j<- 0 hasta m-1
			Escribir sin saltar " " matriz(i,j)
		FinPara
		Escribir ""
	FinPara
	
FinSubProceso


SubProceso  matrizMagica(matriz, n,m)
	Definir i, j, resultado, suma Como Entero
	Definir validacion Como Logico
	
	validacion =Verdadero //siempre inicializa en verdadero?
	resultado= 0 //acumulador
	Para j<- 0 hasta m-1//nose
		resultado = resultado +matriz(0,j) //nose
	FinPara
	escribir resultado
	//como comparo entonces?
	/// suma filas
	Para i<- 0 hasta n-1
		suma = 0 ///reinicio la suma en cada fila
		Para j<- 0 hasta m-1
			suma = suma+matriz(i,j)
		FinPara
		validacion = validacion y resultado==suma//(verdadero es igual a verdadero)  y  resultado es igual a lo que da la suma
	FinPara
	
	
	/// suma columnas
	Para j<- 0 hasta m-1
		suma = 0 ///reinicio la suma en cada columna
		Para i<- 0 hasta n-1
			suma = suma+matriz(i,j)
		FinPara
		validacion = validacion y resultado==suma
	FinPara
	
	
	///suma la diagonal principal
	suma = 0
	Para i<-0 Hasta n-1 Con Paso 1 Hacer
		
        Para j<-0 Hasta m-1 Con Paso 1 Hacer
			si i=j
				suma = suma + matriz(i,j)
			FinSi
		Fin Para
		
    Fin Para
	validacion = validacion y resultado == suma
	
	///suma la diagonal secundaria
	suma = 0
	Para i<-0 Hasta n-1 Con Paso 1 Hacer
		
        Para j<-0 Hasta m-1 Con Paso 1 Hacer
			si i+j=n-1
				suma = suma + matriz(i,j)
			FinSi
		Fin Para
		
    Fin Para
	validacion = validacion y resultado == suma
	
	Si validacion 
		Escribir "la matriz es mágica"
	SiNo
		Escribir "la matriz no es mágica"
	FinSi
	
	
FinSubProceso



SubProceso llenarMatrizMagica(matriz Por Referencia, n, m)
	Definir i, j Como Entero
	matriz(0,0) = 2 
	matriz(0,1) = 7
	matriz(0,2) = 6
	matriz(1,0) = 9
	matriz(1,1) = 5
	matriz(1,2) = 1
	matriz(2,0) = 4
	matriz(2,1) = 3
	matriz(2,2) = 8
FinSubproceso